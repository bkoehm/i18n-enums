buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    }
}
plugins {
    id "com.jfrog.artifactory" version "4.4.0"
}
version project.pluginVersion
group "org.grails.plugins"

apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "org.grails.grails-plugin"
apply plugin: "org.grails.grails-plugin-publish"

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    console "org.grails:grails-console"
    compile 'org.codehaus.groovy:groovy-all'
    profile "org.grails.profiles:plugin"
    provided "org.grails:grails-plugin-domain-class"

    testCompile "org.grails:grails-plugin-testing"
    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
}

grailsPublish {
    user = System.getenv("BINTRAY_USER") ?: project.bintrayUser
    key = System.getenv("BINTRAY_KEY") ?: project.bintrayKey

    githubSlug = 'https://github.com/sbglasius/i18n-enums'
    license {
        name = 'Apache-2.0'
    }
    title = project.name
    desc = "This plugin adds an annotation usable on Enums to easy add and implement the MessageSourceResolvable interface in an standard way throughout a project."
    developers = [sbglasius: "Soeren Berg Glasius"]
    portalUser = System.getenv("GRAILS_PORTAL_USER") ?: project.grailsPortalUser
    portalPassword = System.getenv("GRAILS_PORTAL_PASSWORD") ?: project.grailsPortalPassword
}

artifactory {
    contextUrl = 'http://oss.jfrog.org'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = System.getenv("BINTRAY_USER") ?: project.bintrayUser
            password = System.getenv("BINTRAY_KEY") ?: project.bintrayKey
        }
        defaults {
            publications('maven')
        }
    }
}